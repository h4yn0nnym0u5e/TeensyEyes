// Define if you wish to debug memory usage.  Only works on T4.x
//#define DEBUG_MEMORY

#include <SPI.h>
#include <array>
#include <Wire.h>
#include <Entropy.h>
#include <SD.h>

//#include <TeensyDebug.h>

#include "config.h"
#include "util/logging.h"
#include "sensors/LightSensor.h"
#include "sensors/PersonSensor.h"

#ifdef USE_AUDIO_MEMORY
#include "audio-memory.h"
#endif	/* USE_AUDIO_MEMORY.  */

#ifdef USE_AUDIO_SDCARD
#include "audio-sd.h"
#endif	/* USE_AUDIO_SDCARD.  */

// The index of the currently selected eye definitions
static uint32_t defIndex{0};

#ifdef ORIG_CODE
TwoWire WIRE = Wire2;
#else
TwoWire WIRE = Wire;
#endif
LightSensor lightSensor(LIGHT_PIN);
PersonSensor personSensor(WIRE);
bool personSensorFound = USE_PERSON_SENSOR;

bool hasBlinkButton() {
  return BLINK_PIN >= 0;
}

bool hasLightSensor() {
  return LIGHT_PIN >= 0;
}

bool hasJoystick() {
  return JOYSTICK_X_PIN >= 0 && JOYSTICK_Y_PIN >= 0;
}

bool hasPersonSensor() {
  return personSensorFound;
}

#ifndef ORIG_CODE
static void printEyeName ()
{
  auto &defs = eyeDefinitions.at(defIndex);
  const char *name = defs[0].name;

  if (!name || name[0] == '\0')
    name = "no name";

  Serial.printf ("Eye #%-2d %s\n", (int)defIndex, name);
}
#endif	/* meissner changes.  */

/// INITIALIZATION -- runs once at startup ----------------------------------
void setup() {
  Serial.begin(115200);
  while (!Serial && millis() < 2000);
  delay(200);
  DumpMemoryInfo();
  Serial.println("");
  Serial.println("==============================");
  Serial.println("");
  Serial.println("Init");
  Serial.flush();
  Entropy.Initialize();
  randomSeed(Entropy.random());

  //halt_cpu();

  if (CrashReport)
  {
    Serial.print(CrashReport);
    while (1)
      delay(10);
  }

#if defined(MUX_A)
  pinMode(MUX_A,OUTPUT);
  digitalWrite(MUX_A,HIGH);
#endif // defined(MUX_A)

#if defined(MUX_B)
  pinMode(MUX_B,OUTPUT);
  digitalWrite(MUX_B,HIGH);
#endif // defined(MUX_B)

#if defined(BACKLIGHT_PIN)
  pinMode(BACKLIGHT_PIN,OUTPUT);
  digitalWrite(BACKLIGHT_PIN,HIGH);
#endif // defined(BACKLIGHT_PIN)

#ifndef ORIG_CODE
  Serial.printf("Eye duration = %d\n", (int)((EYE_DURATION_MS + 500) / 1000));

  if (hasLightSensor()) {
    Serial.printf("Light sensor pin is %d\n", LIGHT_PIN);
  }
#endif	/* meissner changes.  */

  if (hasBlinkButton()) {
    pinMode(BLINK_PIN, INPUT_PULLUP);

#ifndef ORIG_CODE
    Serial.printf("Blink pin is %d\n", BLINK_PIN);
#endif	/* meissner changes.  */
  }

  if (hasJoystick()) {
    pinMode(JOYSTICK_X_PIN, INPUT);
    pinMode(JOYSTICK_Y_PIN, INPUT);

#ifndef ORIG_CODE
    Serial.printf("Joystick pins are %d, %d\n", JOYSTICK_X_PIN, JOYSTICK_Y_PIN);
#endif	/* meissner changes.  */
  }

  if (hasPersonSensor()) {
    WIRE.begin();
    personSensorFound = personSensor.isPresent();
    if (personSensorFound) {
      Serial.println("Person Sensor detected");
      personSensor.enableID(false);
      // personSensor.enableLED(false);
      personSensor.setMode(PersonSensor::Mode::Continuous);
    } else {
      Serial.println("No Person Sensor was found!");
    }
  }

  initEyes(!hasJoystick(), !hasBlinkButton(), !hasLightSensor());

#ifndef ORIG_CODE

#ifdef USE_AUDIO_MEMORY
  setup_audio_memory ();
#endif

#ifdef USE_AUDIO_SDCARD
  setup_audio_sd ();
#endif

  Serial.println("");
  printEyeName();
#endif	/* meissner changes.  */
}

void nextEye() {

#ifdef ORIG_CODE
  defIndex = (defIndex + 1) % eyeDefinitions.size();

#else	/* meissner changes.  */
  // For the first pass go through the eyes in linear order.  After the first
  // pass choose the next eye at random.
  static bool in_order = true;
  uint32_t size = eyeDefinitions.size();
  if (in_order && defIndex < size-1) {
    defIndex++;

  } else {
    in_order = false;
    size_t loop_count = 0;
    uint32_t oldIndex = defIndex;
    do {
      defIndex = Entropy.random (0, size - 1);
    } while (defIndex == oldIndex && ++loop_count < 4);
  }
#endif	/* meissner changes.  */

  eyes->updateDefinitions(eyeDefinitions.at(defIndex));

#ifndef ORIG_CODE
  printEyeName();
#endif	/* meissner changes.  */
}

/// MAIN LOOP -- runs continuously after setup() ----------------------------
void loop() {

#ifdef USE_AUDIO_MEMORY
  // Update sounds
  loop_audio_memory ();
#endif

#ifdef USE_AUDIO_SDCARD
  // Update sounds
  loop_audio_sd ();
#endif

  // Switch eyes periodically
  static elapsedMillis eyeTime{};
  if (eyeTime > EYE_DURATION_MS) {
    nextEye();
    eyeTime = 0;
  }

  // Blink on button press
  if (hasBlinkButton() && digitalRead(BLINK_PIN) == LOW) {
    eyes->blink();
  }

  // Move eyes with an analog joystick
  if (hasJoystick()) {
    auto x = analogRead(JOYSTICK_X_PIN);
    auto y = analogRead(JOYSTICK_Y_PIN);
    eyes->setPosition((x - 512) / 512.0f, (y - 512) / 512.0f);
  }

  if (hasLightSensor()) {
    lightSensor.readDamped([](float value) {
      eyes->setPupil(value);
    });
  }

  if (hasPersonSensor() && personSensor.read()) {
    // Find the closest face that is facing the camera, if any
    int maxSize = 0;
    person_sensor_face_t maxFace{};
    
    for (int i = 0; i < personSensor.numFacesFound(); i++) {
      const person_sensor_face_t face = personSensor.faceDetails(i);
      if (face.is_facing && face.box_confidence > 60) {
	Serial.printf ("Face #%d, right = %d, left = %d, top = %d, bottom = %d\n",
		       i,
		       face.box_right,
		       face.box_left,
		       face.box_top,
		       face.box_bottom);

        int size = (face.box_right - face.box_left) * (face.box_bottom - face.box_top);
        if (size > maxSize) {
          maxSize = size;
          maxFace = face;
        }
      }
    }

    if (maxSize > 0) {
      eyes->setAutoMove(false);
      float targetX = -((static_cast<float>(maxFace.box_left) + static_cast<float>(maxFace.box_right - maxFace.box_left) / 2.0f) / 127.5f - 1.0f);
      float targetY = (static_cast<float>(maxFace.box_top) + static_cast<float>(maxFace.box_bottom - maxFace.box_top) / 3.0f) / 127.5f - 1.0f;
      Serial.printf ("Face, targetX = %g, targetY = %g\n\n", targetX, targetY);
      eyes->setTargetPosition(targetX, targetY);
    } else if (personSensor.timeSinceFaceDetectedMs() > 5'000 && !eyes->autoMoveEnabled()) {
      // We haven't seen a face for a while so enable automove
      Serial.println ("Turning off Person sensor");
      eyes->setAutoMove(true);
    }
  }

  eyes->renderFrame();
}
